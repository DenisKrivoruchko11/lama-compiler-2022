-- Stack machine.

import Array;
import List;
import Fun;
import Collection;
import World;
import State;
import Expr;
import Buffer;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  case i of
    READ         -> sprintf ("READ")
  | WRITE        -> sprintf ("WRITE")
  | BINOP (s)    -> sprintf ("BINOP %s", s)
  | LD    (x)    -> sprintf ("LD %s", x)
  | ST    (x)    -> sprintf ("ST %s", x)
  | CONST (n)    -> sprintf ("CONST %d", n)
  | LABEL (s)    -> sprintf ("LABEL %s", s)
  | JMP   (l)    -> sprintf ("JMP %s", l)
  | CJMP  (c, l) -> sprintf ("CJMP %s, %s", c, l)
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Evaluation environment: keeps a mapping between labels and (sub)programs
fun initEvalEnv (insns) {
  var map =
    fix (fun (rec) {
          fun ([m, insns]) {
            case insns of
              {}               -> m
            | LABEL (lab) : tl -> rec ([addMap (m, lab, insns), tl])
            | _ : tl           -> rec ([m, tl])
            esac
          }
        }) $ [emptyMap (compare), insns];
  
  [fun (l) {
     case findMap (map, l) of Some (insns) -> insns esac
  }]
}

-- Accessor function
fun fromLabel (env, lab) {
  env [0] (lab)
}

fun evalIns(env, [stack, st, w], ins, tl) {
  case ins of
    READ              -> [case readWorld (w) of [n, w] -> [n : stack, st, w] esac, tl]
  | WRITE             -> [case stack of top : rest -> [rest, st, writeWorld (top, w)] esac, tl]
  | BINOP (op)        -> [case stack of fst : snd : rest -> [evalBinop (op, snd, fst) : rest, st, w] esac, tl]
  | LD    (name)      -> [[st (name) : stack, st, w], tl]
  | ST    (name)      -> [case stack of top : rest -> [stack, st <- [name, top], w] esac, tl]
  | CONST (v)         -> [[v : stack, st, w], tl]
  | LABEL (_)         -> [[stack, st, w], tl]
  | JMP   (lab)       -> [[stack, st, w], env.fromLabel (lab)]
  | CJMP  (cond, lab) -> [[stack, st, w], case stack of
                                            top : _ -> case cond of
                                                        "z" -> if top == 0 then env.fromLabel (lab) else tl fi
                                                       | _  -> if top != 0 then env.fromLabel (lab) else tl fi
                                                       esac
                                          esac]
  esac
}

-- Stack machine interpreter. Takes an environment, an SM-configuration and a program,
-- returns a final configuration
fun eval (env, c, insns) {
  case insns of
    {} -> c
  | h : tl -> case evalIns (env, c, h, tl) of [state, newTail] -> eval (env, state, newTail) esac
  esac
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval (initEvalEnv (insns), [{}, emptyState, createWorld (input)], insns)[2].getOutput
}

-- Compilation environment: generates labels
fun makeCompEnv (n) {
  [fun () {
     [sprintf ("L%d", n), makeCompEnv (n+1)]
  }]
}

fun initCompEnv () {
  makeCompEnv (0)
}

-- Accessor function: generates one label
fun genLabel (env) {
  env [0] ()
}

-- Utility function: generates n labels
fun genLabels (env, n) {
  fun inner (env, n) {
    if n == 0
    then {env}
    else case env.genLabel of
           [lab, env] -> lab : inner (env, n-1)
         esac
    fi
  }

  listArray (inner (env, n))
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine instructions
fun compileExpr (expr) {
  case expr of
    Var   (name)            -> singletonBuffer (LD (name))
  | Const (v)               -> singletonBuffer (CONST (v))
  | Binop (op, left, right) -> compileExpr (left) <+> compileExpr (right) <+ BINOP (op)
  esac
}

-- Compiles a statement into a stack machine code.
-- Takes a statement, returns a list of stack machine
-- instructions.
public fun compileSM (stmt) {
  -- Generates a buffer with an optional label
  --    lab     --- the name of the label
  --    labUsed --- a flag if the lebel is needed
  fun label (lab, labUsed) {
    if labUsed
    then singletonBuffer (LABEL (lab))
    else emptyBuffer ()
    fi
  }

  -- Generates SM code for a given statement
  --    lab  --- a label th go to after the statement is executed
  --    env  --- compilation environment
  --    stmt --- a statement to generate SM code for
  -- Returns a triple:
  --    boolean flag indicating if "lab" was used
  --    updated environment
  --    code buffer  
  fun compile (lab, env, stmt) {
    case stmt of
      Skip           -> [false, env, emptyBuffer ()]
    | Read  (x)      -> [false, env, singletonBuffer (READ) <+ ST (x)]
    | Write (e)      -> [false, env, compileExpr (e) <+ WRITE]
    | Assn  (x, e)   -> [false, env, compileExpr (e) <+ ST (x)]
    | Seq   (s1, s2) ->
        case env.genLabel of
          [s2Lab, env] ->
             case compile (s2Lab, env, s1) of
               [s2LabUsed, env, s1Code] ->
                  case compile (lab, env, s2) of
                    [labUsed, env, s2Code] -> [labUsed, env, s1Code <+> label (s2Lab, s2LabUsed) <+> s2Code]
                  esac
             esac
        esac
    | If (cond, fst, snd) -> 
      case env.genLabel of
       [elseLab, env] ->
        case compile (lab, env, fst) of
          [_, env, thenCode] ->
            case compile (lab, env, snd) of
              [_, env, elseCode] ->
                [true, env, compileExpr (cond) <+ CJMP ("z", elseLab) <+> thenCode 
                            <+ JMP (lab) <+ LABEL (elseLab) <+> elseCode]
            esac
        esac
      esac
    | While (cond, body) ->
      case env.genLabels (2) of
        [bodyLab, condLab, env] ->
          case compile (condLab, env, body) of
            [_, env, bodyCode] ->
              [false, env, singletonBuffer (JMP (condLab)) <+ LABEL (bodyLab) <+> bodyCode 
                           <+ LABEL (condLab) <+> compileExpr (cond) <+ CJMP ("nz", bodyLab)]
          esac
      esac
    | DoWhile (body, cond) ->
      case env.genLabels (2) of
        [condLab, bodyLab, env] ->
          case compile (condLab, env, body) of
            [condLabUsed, env, bodyCode] ->
              [false, env, singletonBuffer (LABEL (bodyLab)) <+> bodyCode <+> label (condLab, condLabUsed) 
                           <+> compileExpr (cond) <+ CJMP ("nz", bodyLab)]
          esac
      esac
    esac
  }  

  case initCompEnv ().genLabel of
    [endLab, env] ->
       case compile (endLab, env, stmt) of
         [endLabUsed, _, code] -> getBuffer $  code <+> label (endLab, endLabUsed)
       esac
  esac
}
